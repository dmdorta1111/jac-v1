BEGIN_GUI_DESCR

	SHOW_PARAM	STRING		ITEM_NUM
	SHOW_PARAM	STRING		SUB_TYPE
	SHOW_PARAM	INTEGER		OPENING_TYPE
	SHOW_PARAM	STRING		DOOR_ITEM_DESC
	SHOW_PARAM	STRING		FRAME_ITEM_DESC
	SHOW_PARAM	STRING		AUTO_BOTT_APPLICATION

	USER_INPUT_PARAM STRING	AUTO_BOTT_MANUFACTURER
	USER_INPUT_PARAM STRING	AUTO_BOTT_MODEL
	USER_INPUT_PARAM DOUBLE	AUTO_BOTT_WIDTH
	USER_INPUT_PARAM DOUBLE	AUTO_BOTT_DEPTH
	USER_INPUT_PARAM DOUBLE	BOTTOM_CHANNEL_THICKNESS
	
	IF PLUNGER_HOLE == 1
		USER_INPUT_PARAM DOUBLE	PLUNGER_HOLE_DIA
		USER_INPUT_PARAM DOUBLE	PLUNGER_HOLE_LOC
		!PLUNGER_HOLE_LOC IS FROM THE TOP OF THE DEVICE
	END_IF
	
	CHECKBOX_PARAM INTEGER PLUNGER_HOLE "YES/NO"
	END_GUI_DESCR

BEGIN_ASM_DESCR

	DECLARE_VARIABLE	INTEGER	OPERATION
	DECLARE_VARIABLE	STRING	AUTO_BOTT_APPLICATION
	DECLARE_VARIABLE	DOUBLE	AUTO_BOTT_WIDTH
	DECLARE_VARIABLE	DOUBLE	AUTO_BOTT_DEPTH
	DECLARE_VARIABLE	STRING	AUTO_BOTT_MANUFACTURER
	DECLARE_VARIABLE	STRING	AUTO_BOTT_MODEL
	DECLARE_VARIABLE	STRING	AUTO_BOTT_DESC
	DECLARE_VARIABLE	DOUBLE	BOTTOM_CHANNEL_THICKNESS
	DECLARE_VARIABLE	INTEGER PLUNGER_HOLE
	DECLARE_VARIABLE	DOUBLE	PLUNGER_HOLE_DIA
	DECLARE_VARIABLE	DOUBLE	PLUNGER_HOLE_LOC

	IF OPERATION == 0
		CONFIG_ELEM
		AUTO_BOTT_DESC = AUTO_BOTT_MODEL + " MORTISED DOOR BOTTOM" 
	END_IF

	IF OPERATION == 1
		IF	HAND_ == "LHR" OR HAND_ == "RH" OR \
			HAND_ == "LHR1" OR HAND_ == "LHRA1" OR HAND_ == "LHRI1" OR \
			HAND_ == "RH2" OR HAND_ == "RHA2" OR HAND_ == "RHI2"

			SEARCH_MDL_REF DOOR_ASM CSYS "ARMOR1" ASM_CSYS
			CREATE_UDF udf\csys DOOR_ASM
			UDF_REF ATTACH ASM_CSYS
			UDF_DIM	ROTX 0
			UDF_DIM	ROTY 180
			UDF_DIM	ROTZ 0
			UDF_DIM	LOCX 0
			UDF_DIM	LOCY UNDERCUT+BOTTOM_CHANNEL_DEPTH-PLUNGER_HOLE_LOC-BOTTOM_CHANNEL_THICKNESS
			UDF_DIM	LOCZ 0
			UDF_EXP_REF ATTACH CSYS 0
			END_CREATE_UDF
		END_IF

		IF	HAND_ == "RHR" OR HAND_ == "LH" OR \
			HAND_ == "RHR2" OR HAND_ == "RHRA2" OR HAND_ == "RHRI2" OR \
			HAND_ == "LH1" OR HAND_ == "LHA1" OR HAND_ == "LHI1"

			SEARCH_MDL_REF DOOR_ASM CSYS "ARMOR2" ASM_CSYS
			CREATE_UDF udf\csys DOOR_ASM
			UDF_REF ATTACH ASM_CSYS
			UDF_DIM	ROTX 0
			UDF_DIM	ROTY 180
			UDF_DIM	ROTZ 0
			UDF_DIM	LOCX 0
			UDF_DIM	LOCY UNDERCUT+BOTTOM_CHANNEL_DEPTH-PLUNGER_HOLE_LOC-BOTTOM_CHANNEL_THICKNESS
			UDF_DIM	LOCZ 0
			UDF_EXP_REF ATTACH CSYS 0
			END_CREATE_UDF
		END_IF
		
		IF PLUNGER_HOLE == 1
			CREATE_UDF udf\hole DOOR_ASM
				UDF_REF ATTACH ATTACH
				UDF_DIM	DIA PLUNGER_HOLE_DIA
				UDF_DIM	DEPTH SM_THICK
				UDF_INTERSECTION * PART_LEVEL
			END_CREATE_UDF	
		END_IF	
			
	END_IF		
END_ASM_DESCR
