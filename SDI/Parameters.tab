BEGIN_GUI_DESCR

USER_INPUT_PARAM STRING PARAMETER_NAME REQUIRED

RADIOBUTTON_PARAM INTEGER OPERATION "NEW" "REPLACE" "DELETE"

RADIOBUTTON_PARAM INTEGER MODEL_TYPE "PART" "ASSEMBLY" "BOTH"
RADIOBUTTON_PARAM INTEGER PARAMETER_TYPE "DOUBLE" "INTEGER" "STRING"


CHECKBOX_PARAM INTEGER USER_INPUT "YES/NO"
CHECKBOX_PARAM INTEGER ADD_TO_FAMILY_TABLE "YES/NO"


END_GUI_DESCR

BEGIN_ASM_DESCR
ENABLE_DATA_CAPTURE_MODE FALSE

DECLARE_ARRAY FILES
DECLARE_ARRAY FILES1
DECLARE_ARRAY PARAMS

CONFIG_ELEM

GET_WORKING_DIRECTORY dir
CLEAR_ARRAY FILES
CLEAR_ARRAY FILES1

IF MODEL_TYPE == 0
	READ_DIRECTORY dir "*.prt" FILES
END_IF

IF MODEL_TYPE == 1
	READ_DIRECTORY dir "*.asm" FILES
END_IF

IF MODEL_TYPE == 2
	READ_DIRECTORY dir "*.prt" FILES
	READ_DIRECTORY dir "*.asm" FILES1
	FOR FILE1 REF ARRAY FILES1
		ADD_ARRAY_ELEM FILES FILE1
	END_FOR
END_IF
	
GET_ARRAY_SIZE FILES sizeFILES


IF sizeFILES > 0 AND (OPERATION == 1 OR OPERATION == 2)
	FOR FILE REF ARRAY FILES
		CLEAR_CATCH_ERROR
		RETRIEVE_MDL &FILE ITEM
		BEGIN_CATCH_ERROR
			DELETE_MDL_PARAM ITEM PARAMETER_NAME
		END_CATCH_ERROR
		SAVE_MDL ITEM
		WINDOW_CLOSE
		ERASE_NOT_DISPLAYED
	END_FOR
END_IF


IF sizeFILES > 0 AND (OPERATION == 0 OR OPERATION == 1)
	FOR FILE REF ARRAY FILES
		RETRIEVE_MDL &FILE ITEM
		CLEAR_ARRAY PARAMS
		SEARCH_MDL_PARAM_NAMES ITEM PARAMETER_NAME PARAMS
		GET_ARRAY_SIZE PARAMS sizePARAMS
		IF sizePARAMS < 1
			PRINT "%" FILE
			IF PARAMETER_TYPE == 0 AND USER_INPUT == 1
				USER_INPUT_PARAM DOUBLE DOUBLE_VALUE
				CREATE_MDL_PARAM ITEM PARAMETER_NAME DOUBLE DOUBLE_VALUE
			ELSE_IF PARAMETER_TYPE == 0 AND USER_INPUT == 0
				CREATE_MDL_PARAM ITEM PARAMETER_NAME DOUBLE 0
			END_IF

			IF PARAMETER_TYPE == 1 AND USER_INPUT == 1
				USER_INPUT_PARAM INTEGER INTEGER_VALUE
				CREATE_MDL_PARAM ITEM PARAMETER_NAME INTEGER INTEGER_VALUE
			ELSE_IF PARAMETER_TYPE == 1 AND USER_INPUT == 0
				CREATE_MDL_PARAM ITEM PARAMETER_NAME INTEGER 0
			END_IF
			IF PARAMETER_TYPE == 2 AND USER_INPUT == 1
				USER_INPUT_PARAM STRING STRING_VALUE
				CREATE_MDL_PARAM ITEM PARAMETER_NAME STRING STRING_VALUE
			ELSE_IF PARAMETER_TYPE == 2 AND USER_INPUT == 0
				CREATE_MDL_PARAM ITEM PARAMETER_NAME STRING "XX"
			END_IF
			IF ADD_TO_FAMILY_TABLE == 1
				BEGIN_CATCH_ERROR
					GET_REF_FAMINSTANCE ITEM "*" INST_REF
					IF INST_REF <> ""
						SEARCH_MDL_PARAM ITEM "USED" USED
						INSERT_FAMITEM ITEM USED
					END_IF
				END_CATCH_ERROR
			END_IF
			SAVE_MDL ITEM
		END_IF
	END_FOR
END_IF

WINDOW_CLOSE
ERASE_NOT_DISPLAYED

MESSAGE_BOX "FINISHED"
	
END_ASM_DESCR

