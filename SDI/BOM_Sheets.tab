BEGIN_GUI_DESCR

USER_INPUT_PARAM STRING SO_NUM REQUIRED

	IF PROCESS_TYPE_ == 0
		IF DOOR_WO == ""
			USER_INPUT_PARAM STRING DOOR_WO REQUIRED
		END_IF
		WO_NUM = DOOR_WO
	ELSE_IF PROCESS_TYPE_ == 1
		IF FRAME_WO == ""
			USER_INPUT_PARAM STRING FRAME_WO REQUIRED
		END_IF
		WO_NUM = FRAME_WO
	END_IF

RADIOBUTTON_PARAM INTEGER PROCESS_TYPE_ "DOORS" "FRAMES" "LITES"

END_GUI_DESCR

BEGIN_ASM_DESCR

DECLARE_VARIABLE STRING EXCEL_DOCUMENT
DECLARE_VARIABLE STRING SO_NUM
DECLARE_VARIABLE STRING PROJECT_FOLDER
DECLARE_VARIABLE STRING DXF_FOLDER
DECLARE_VARIABLE STRING FRAME_WO ""
DECLARE_VARIABLE STRING DOOR_WO ""
DECLARE_VARIABLE STRING EOF
DECLARE_VARIABLE INTEGER ROW
DECLARE_VARIABLE STRING EXCEL_COMPONENT_NAME ""	
DECLARE_VARIABLE INTEGER STOCKTYPE
DECLARE_VARIABLE DOUBLE THICKNESS
DECLARE_VARIABLE STRING SHAPE
DECLARE_VARIABLE STRING COMPONENT_NAME	
DECLARE_VARIABLE STRING WO_NUM ""
DECLARE_VARIABLE STRING PROCESS_TYPE ""
DECLARE_VARIABLE STRING STATE
DECLARE_VARIABLE DOUBLE X 0
DECLARE_VARIABLE DOUBLE Y 0
DECLARE_VARIABLE DOUBLE Z 0
DECLARE_VARIABLE DOUBLE LENGTH
DECLARE_VARIABLE DOUBLE WIDTH
DECLARE_VARIABLE INTEGER ROWS
DECLARE_VARIABLE STRING SWIDTH
DECLARE_VARIABLE STRING SLENGTH
DECLARE_VARIABLE INTEGER SHEET
DECLARE_VARIABLE STRING CUSTOMER
DECLARE_VARIABLE STRING JOB_NAME
DECLARE_VARIABLE STRING SUB_JOB_NAME
DECLARE_VARIABLE STRING ITEM_NUM
DECLARE_VARIABLE STRING ITEM_DESC
DECLARE_VARIABLE STRING SINDEX
DECLARE_ARRAY CSYS_ARRAY
DECLARE_ARRAY SURFACE_ARRAY

DISPLAY_DATUM PLANES FALSE
DISPLAY_DATUM AXES FALSE
DISPLAY_DATUM POINTS FALSE
DISPLAY_DATUM CSYS FALSE	

SO_NUM = "27655"	
FRAME_WO = "57454"
DOOR_WO = "57458"

! CONFIG_ELEM

PROJECT_FOLDER = "C:"+\+"00WF4"+\+SO_NUM+\+"ProE Models"
DXF_FOLDER = PROJECT_FOLDER+\+WO_NUM

SET_WORKING_DIRECTORY PROJECT_FOLDER	
CREATE_DIRECTORY DXF_FOLDER

EXCEL_DOCUMENT = "BOM_"+SO_NUM+".xls"		

EXCEL_CONNECT
EXCEL_LOAD_DOCUMENT EXCEL_DOCUMENT

RETRIEVE_MDL assem.asm ASSEM
SET_MDL_NAME ASSEM "WO_"+WO_NUM
SET_MDL_PARAM ASSEM "CUSTOMER" ""
SET_MDL_PARAM ASSEM "JOB_NAME" ""
SET_MDL_PARAM ASSEM "SO_NUM" SO_NUM
SET_MDL_PARAM ASSEM "WO_NUM" WO_NUM
SET_MDL_PARAM ASSEM "ITEM_NUM" " "
SET_MDL_PARAM ASSEM "ITEM_DESC" "SEE TABLE"

CREATE_DRW ASSEM material_req_template.drw refDrawing		

CREATE_DRW_TABLE refDrawing ASSEM 1 .4375 6.625  "lib:mat_body.tbl"  BODY

IF PROCESS_TYPE_ == 0
	EXCEL_ACTIVATE_SHEET SHEET_BY_NAME "DOOR"		
ELSE_IF PROCESS_TYPE_ == 1
	EXCEL_ACTIVATE_SHEET SHEET_BY_NAME "FRAME"
END_IF	

ROW = 5
EXCEL_GET_VALUE ROW 1 EXCEL_COMPONENT_NAME


IF EXCEL_COMPONENT_NAME == ""
	EOF = "TRUE"
ELSE
	EOF = "FALSE"
END_IF

DECLARE_ARRAY MACRO
EXCEL_RUN_MACRO "macro1" MACRO

ROW = 5
EXCEL_GET_VALUE ROW 1 EXCEL_COMPONENT_NAME
IF EXCEL_COMPONENT_NAME <> ""
	EOF = "FALSE"
	WHILE EOF == "FALSE"
		ROW++		
		EXCEL_GET_VALUE ROW 1 EXCEL_COMPONENT_NAME
		IF EXCEL_COMPONENT_NAME == ""
			EOF = "TRUE"
		ELSE
			EOF = "FALSE"
		END_IF
	END_WHILE

	EXCEL_GET_VALUES CELL_BY_INDEX 5 0 CELL_BY_INDEX (ROW-1) 15 arrayData

	DECLARE_ARRAY data
	DECLARE_ARRAY ITEMS

	FOR data REF ARRAY arrayData
		GET_ARRAY_ELEM data 2 value
		ADD_ARRAY_ELEM ITEMS value
	END_FOR	

	GET_ARRAY_SIZE ITEMS SIZE
	EXCEL_SAVE_DOCUMENT EXCEL_DOCUMENT
	EXCEL_DISCONNECT

	SWITCH_TO_MDL refDrawing

	ERASE_NOT_DISPLAYED

		CUSTOMER = "WBH"
		ITEM_DESC = "FRAME SMALL PARTS"	
		ITEM_NUM = "12345"
		JOB_NAME = "GUAM"
		SUB_JOB_NAME = "HOSPITAL"
		
	DECLARE_VARIABLE INTEGER ITP
	DECLARE_VARIABLE INTEGER ITEM_INDEX
	DECLARE_VARIABLE INTEGER MAX_LINES 26
	DECLARE_VARIABLE INTEGER TEMPD
	DECLARE_VARIABLE INTEGER TEMPN
	DECLARE_VARIABLE INTEGER NUMBER
	DECLARE_VARIABLE INTEGER DENOMENATOR 16
	DECLARE_VARIABLE INTEGER QTY
	DECLARE_VARIABLE INTEGER LINE_INDEX
	DECLARE_VARIABLE STRING MAT_NUM
	DECLARE_VARIABLE INTEGER SHEET_INDEX
	DECLARE_VARIABLE INTEGER TOTAL_SHEETS

	ITP = SIZE/16
	SHEET = 1
	ITEM_INDEX = 1
	SHEET_INDEX = 1
	MAT_NUM = "XXX"
	GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+2) STOCKTYPE

	WHILE ITEM_INDEX <= ITP
		IF STOCKTYPE == 1
			IF SHEET_INDEX > 1
				SHEET = SHEET_INDEX
				INSERT_DRW_SHEET refDrawing SHEET_INDEX
				SET_DRW_CUR_SHEET refDrawing SHEET_INDEX
				SET_DRW_FORMAT refDrawing SHEET_INDEX material_req_format.frm
				CREATE_DRW_TABLE refDrawing ASSEM SHEET_INDEX .4375 6.625  "lib:mat_body.tbl"  BODY
			END_IF

			CREATE_DRW_NOTE refDrawing 1.0625 7.6875 WO_NUM
			CREATE_DRW_NOTE refDrawing 2.8125 7.6875 "&todays_date" HORIZONTAL CENTER

			IF CUSTOMER == ""
				PRINT "ENTER CUSTOMER NAME"
				USER_INPUT_PARAM STRING CUSTOMER
			END_IF
			CREATE_DRW_NOTE refDrawing 1.9375 7.125 CUSTOMER HORIZONTAL CENTER

			IF JOB_NAME == ""
				PRINT "ENTER JOB/PROJECT NAME"
				USER_INPUT_PARAM STRING JOB_NAME
			END_IF

			IF SUB_JOB_NAME == ""
				PRINT "ENTER SUB_JOB NAME"
				USER_INPUT_PARAM STRING SUB_JOB_NAME
			END_IF

			IF SUB_JOB_NAME == ""
				CREATE_DRW_NOTE refDrawing 5.5 7.125 JOB_NAME HORIZONTAL CENTER
			ELSE
				CREATE_DRW_NOTE refDrawing 5.5 7.209 JOB_NAME HORIZONTAL CENTER
				CREATE_DRW_NOTE refDrawing 5.5 7.042 SUB_JOB_NAME HORIZONTAL CENTER
			END_IF

			CREATE_DRW_NOTE refDrawing 9.5 7.9375 itos(SHEET)

			IF ITEM_NUM == ""
				PRINT "ENTER ITEM NUMBER"
				USER_INPUT_PARAM STRING ITEM_NUM
			END_IF
			CREATE_DRW_NOTE refDrawing 8.125 7.6875 ITEM_NUM

			IF ITEM_DESC == ""
				PRINT "ENTER ITEM DESCRIPTION"
				USER_INPUT_PARAM STRING ITEM_DESC
			END_IF
			CREATE_DRW_NOTE refDrawing 9.0625 7.125 ITEM_DESC HORIZONTAL CENTER
			
			LINE_INDEX = 1
			WHILE ITEM_INDEX <= ITP AND LINE_INDEX <= MAX_LINES
				GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+14) MatNuM
				GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+2) STOCKTYPE
				GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+13) SHAPE2	
				IF LINE_INDEX == 1 AND STOCKTYPE == 1 AND (SHAPE2 == "1*" OR SHAPE2 == "2*")
					GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+14) MAT_NUM		
					GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+15) MAT_DESC
					SET_DRW_TABLE_TEXT refDrawing BODY LINE_INDEX 1 MAT_NUM+"  -  "+MAT_DESC
					SET_DRW_TABLE_FORMAT refDrawing BODY LINE_INDEX 1 HORIZONTAL CENTER VERTICAL MIDDLE
					LINE_INDEX = LINE_INDEX + 1				
				END_IF
				IF MAT_NUM <> MatNum AND STOCKTYPE == 1 AND (SHAPE2 == "1*" OR SHAPE2 == "2*")
					IF LINE_INDEX > 1 AND LINE_INDEX <= MAX_LINES - 4
						MERGE_DRW_TABLE refDrawing BODY LINE_INDEX 1 LINE_INDEX 8
						LINE_INDEX = LINE_INDEX + 1
						MERGE_DRW_TABLE refDrawing BODY LINE_INDEX 1 LINE_INDEX 8
						GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+14) MAT_NUM		
						GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+15) MAT_DESC
						SET_DRW_TABLE_TEXT refDrawing BODY LINE_INDEX 1 MAT_NUM+"  -  "+MAT_DESC
						SET_DRW_TABLE_FORMAT refDrawing BODY LINE_INDEX 1 HORIZONTAL CENTER VERTICAL MIDDLE
						LINE_INDEX = LINE_INDEX + 1
					ELSE_IF LINE_INDEX > 1 AND LINE_INDEX > MAX_LINES - 4
						LINE_INDEX = MAX_LINES+1								
					END_IF
				END_IF
				
				IF LINE_INDEX <= MAX_LINES AND STOCKTYPE == 1 AND (SHAPE2 == "1*" OR SHAPE2 == "2*")			
					GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+0) INDEX
					
					SINDEX = itos(INDEX)
					IF strlen(SINDEX) == 1
						SINDEX = "00"+SINDEX
					ELSE_IF strlen(SINDEX) == 2
						SINDEX = "0"+SINDEX
					END_IF
					
					SET_DRW_TABLE_TEXT refDrawing BODY LINE_INDEX 1 SINDEX
					SET_DRW_TABLE_FORMAT refDrawing BODY LINE_INDEX 1 HORIZONTAL CENTER VERTICAL MIDDLE HEIGHT .1

					GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+1) DESC
					SET_DRW_TABLE_TEXT refDrawing BODY LINE_INDEX 2 DESC
					SET_DRW_TABLE_FORMAT refDrawing BODY LINE_INDEX 2 VERTICAL MIDDLE HEIGHT .1


					GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+3) QTY
					SET_DRW_TABLE_TEXT refDrawing BODY LINE_INDEX 3 itos(QTY)
					SET_DRW_TABLE_FORMAT refDrawing BODY LINE_INDEX 3 HORIZONTAL CENTER VERTICAL MIDDLE HEIGHT .1

						GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+8) SWIDTH
							WIDTH = stof(SWIDTH)
							TEMPD = 1/DENOMENATOR
							TEMPN = ROUND((WIDTH / TEMPD),0)
							NUMBER = FLOOR(TEMPN / DENOMENATOR)
							TEMPN = TEMPN - (NUMBER*DENOMENATOR)
							TEMPD = DENOMENATOR
							WHILE MOD(TEMPN,2) == 0 AND MOD(TEMPD,2) == 0
								TEMPN = TEMPN/2
								TEMPD = TEMPD/2
							END_WHILE						
							
							IF NUMBER > 0 AND TEMPN > 0
								SWIDTH = itos(NUMBER) +"-"+ftos(TEMPN,0)+"/"+ftos(TEMPD,0)
							ELSE_IF NUMBER > 0 AND TEMPN == 0
								SWIDTH = itos(NUMBER)
							ELSE
								SWIDTH = ftos(TEMPN,0)+"/"+ftos(TEMPD,0)
							END_IF

						GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+9) SLENGTH
							LENGTH = stof(SLENGTH)
							TEMPD = 1/DENOMENATOR
							TEMPN = ROUND((LENGTH / TEMPD),0)
							NUMBER = FLOOR(TEMPN / DENOMENATOR)
							TEMPN = TEMPN - (NUMBER*DENOMENATOR)
							TEMPD = DENOMENATOR
							WHILE MOD(TEMPN,2) == 0 AND MOD(TEMPD,2) == 0
								TEMPN = TEMPN/2
								TEMPD = TEMPD/2
							END_WHILE						
							
							IF NUMBER > 0 AND TEMPN > 0
								SLENGTH = itos(NUMBER) +"-"+ftos(TEMPN,0)+"/"+ftos(TEMPD,0)
							ELSE_IF NUMBER > 0 AND TEMPN == 0
								SLENGTH = itos(NUMBER)
							ELSE
								SLENGTH = ftos(TEMPN,0)+"/"+ftos(TEMPD,0)
							END_IF
							
						GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+10) GRAIN
						IF GRAIN == "0"
							SET_DRW_TABLE_TEXT refDrawing BODY LINE_INDEX 5 "  "+SWIDTH+"   x   "+SLENGTH
						ELSE_IF GRAIN == "1"
							SET_DRW_TABLE_TEXT refDrawing BODY LINE_INDEX 5 "  "+SWIDTH+"   x   "+SLENGTH+"  **"
						ELSE_IF GRAIN == "2"
							SET_DRW_TABLE_TEXT refDrawing BODY LINE_INDEX 5 "  "+SLENGTH+"   x   "+SWIDTH+"  **"
						END_IF
						SET_DRW_TABLE_FORMAT refDrawing BODY LINE_INDEX 5 VERTICAL MIDDLE HEIGHT .1
						
					GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+11) PVC
					IF PVC <> "0"
						SET_DRW_TABLE_TEXT refDrawing BODY LINE_INDEX 6 "*"
					END_IF

					GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+12) DEBUR
					IF DEBUR <> "0"
						SET_DRW_TABLE_TEXT refDrawing BODY LINE_INDEX 6 "*"
					END_IF
				END_IF
				LINE_INDEX++
				ITEM_INDEX++
			END_WHILE
		END_IF
		
		IF ITEM_INDEX <= ITP
			GET_ARRAY_ELEM ITEMS (((ITEM_INDEX-1)*16)+2) STOCKTYPE	
			SHEET_INDEX++
			LINE_INDEX = 1
		END_IF
	END_WHILE

	TOTAL_SHEETS = SHEET_INDEX
	SHEET_INDEX = 1
	WHILE SHEET_INDEX <= TOTAL_SHEETS
		SET_DRW_CUR_SHEET refDrawing SHEET_INDEX
		CREATE_DRW_NOTE refDrawing 10.125 7.9375 itos(TOTAL_SHEETS)
		SHEET_INDEX++
	END_WHILE
END_IF
END_ASM_DESCR
	